plugins {
    id "com.github.ben-manes.versions" version "0.42.0"
}

apply plugin: "java-library"
apply plugin: "jacoco"

group = "nl.colorize"
version = "2022.1"
sourceCompatibility = "17"
targetCompatibility = "17"
compileJava.options.encoding = "UTF-8"
sourceSets.main.java.srcDirs = ["source"]
sourceSets.main.resources.srcDirs = ["resources"]
sourceSets.test.java.srcDirs = ["test"]

repositories {
    mavenCentral()
    maven {
        url "https://teavm.org/maven/repository"
    }
    maven {
        url "https://jitpack.io"
    }
}

dependencies {
    api "nl.colorize:colorize-java-commons:2022.8"

    implementation "com.badlogicgames.gdx:gdx:1.11.0"
    implementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:1.11.0"
    implementation "com.badlogicgames.gdx:gdx-platform:1.11.0:natives-desktop"
    implementation "com.badlogicgames.gdx:gdx-freetype:1.11.0"
    implementation "com.badlogicgames.gdx:gdx-freetype-platform:1.11.0:natives-desktop"

    implementation "org.teavm:teavm:0.7.0-dev-1201"
    implementation "org.teavm:teavm-jso:0.7.0-dev-1201"
    implementation "org.teavm:teavm-jso-apis:0.7.0-dev-1201"
    implementation "org.teavm:teavm-classlib:0.7.0-dev-1201"
    implementation "org.teavm:teavm-tooling:0.7.0-dev-1201"

    implementation "javazoom:jlayer:1.0.1"
    implementation "org.java-websocket:Java-WebSocket:1.5.3"
    implementation "org.slf4j:slf4j-jdk14:1.7.36"
    implementation "com.github.mgsx-dev.gdx-gltf:gltf:1.0.0"

    testImplementation "org.junit.jupiter:junit-jupiter:5.8.2"
}

test {
    useJUnitPlatform()
}

jacocoTestReport {
    afterEvaluate {
        classDirectories.from = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ["**/java2d/**", "**/libgdx/**", "**/teavm/**", "**/demo/**"])
        })
    }
}

task coverage(dependsOn: ["test", "jacocoTestReport"]) {
    doLast {
        def report = file("${buildDir}/reports/jacoco/test/html/index.html").text
        def coverage = report =~ "tfoot.*?ctr2.*?>([\\d.]+%).*?tfoot"
        println "-" * 40
        println "Test coverage: " + coverage[0][1]
    }
}

task transpileDemoApplication2D(type: JavaExec, dependsOn: ["compileJava", "processResources"]) {
    classpath = sourceSets.main.runtimeClasspath
    main = "nl.colorize.multimedialib.tool.TeaVMTranspilerTool"
    args "-project", "MultimediaLib-TeaVM-Demo",
         "-resources", "resources",
         "-renderer", "canvas",
         "-out", "${buildDir}/browserdemo2d",
         "-main", "nl.colorize.multimedialib.tool.TeaDemo2D"
}

task transpileDemoApplication3D(type: JavaExec, dependsOn: ["compileJava", "processResources"]) {
    classpath = sourceSets.main.runtimeClasspath
    main = "nl.colorize.multimedialib.tool.TeaVMTranspilerTool"
    args "-project", "MultimediaLib-TeaVM-Demo",
         "-resources", "resources",
         "-renderer", "three",
         "-out", "${buildDir}/browserdemo2d",
         "-main", "nl.colorize.multimedialib.tool.TeaDemo3D"
}
